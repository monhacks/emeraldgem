
text BerryTree_Text_ItsSoftLoamySoil {
	poryswitch(LANGUAGE) {
		EN {
			format("It's a very soft and fertile soil.")
		}
		ES {
			format("Es un suelo blando y fértil.")
		}
	}
}

text BerryTree_Text_WantToPlant {
	poryswitch(LANGUAGE) {
		EN {
			format("It's a very soft and fertile soil.\n"
			"Want to plant a Berry?")
		}
		ES {
			format("Es un suelo blando y fértil.\n"
			"¿Quieres plantar una baya?")
		}
	}
}

text BerryTree_Text_WantToPlantShort {
	poryswitch(LANGUAGE) {
		EN {
			format("Want to plant a Berry?")
		}
		ES {
	format("¿Quieres plantar una baya?")
		}
	}
}

text BerryTree_Text_WantToMulch {
	poryswitch(LANGUAGE) {
		EN {
			format("It's a very soft and fertile soil.\n"
			"Want to mix in some Mulch?")
		}
		ES {
			format("Es un suelo blando y fértil.\n"
			"¿Quieres aplicarle abono?")
		}
	}
}

text BerryTree_Text_PlantedOneBerry {
	poryswitch(LANGUAGE) {
		EN {
			format("{PLAYER} planted a {STR_VAR_1}\n"
			"in the soft and fertile soil.")
		}
		ES {
			format("{PLAYER} plantó una {STR_VAR_1}\n"
			"en el blando y fértil suelo.")
		}
	}
}

text BerryTree_Text_BerryGrowthStage1 {
	poryswitch(LANGUAGE) {
		EN {
			format("A {STR_VAR_1} was planted with {STR_VAR_3}")
		}
		ES {
			format("Una {STR_VAR_1} fue plantada con {STR_VAR_3}.")
		}
	}
}

text BerryTree_Text_BerryGrowthStage2 {
	poryswitch(LANGUAGE) {
		EN {
			format("{STR_VAR_1} sprouted.")
		}
		ES {
			format("{STR_VAR_1} ha crecido.")
		}
	}
}

text BerryTree_Text_BerryGrowthStage3 {
	poryswitch(LANGUAGE) {
		EN {
			format("{STR_VAR_1}'s tree\nis growing well.")
		}
		ES {
			format("El árbol de {STR_VAR_1}\nestá creciendo.")
		}
	}
}

text BerryTree_Text_BerryGrowthStage4 {
	poryswitch(LANGUAGE) {
		EN {
			format("{STR_VAR_1}'s flowers are {STR_VAR_2}.")
		}
		ES {
			format("Las flores de {STR_VAR_1}\n"
			"están {STR_VAR_2}.")
		}
	}
}

text BerryTree_Text_CareAdverbGreat {
	poryswitch(LANGUAGE) {
		EN {
			format("beautiful")
		}
		ES {
			format("hermosas")
		}
	}
}

text BerryTree_Text_CareAdverbPoor {
	poryswitch(LANGUAGE) {
		EN {
			format("ugly")
		}
		ES {
			format("feas")
		}
	}
}

text BerryTree_Text_CareAdverbGood {
	poryswitch(LANGUAGE) {
		EN {
			format("pretty")
		}
		ES {
			format("lindas")
		}
	}
}

text BerryTree_Text_WantToPick {
	poryswitch(LANGUAGE) {
		EN {
			format("{STR_VAR_2} {STR_VAR_1}!\p"
			"Wanna pick them up?")
		}
		ES {
			format("¡{STR_VAR_2} {STR_VAR_1}!\p"
			"¿Te llevarías algunas\n"
			"{STR_VAR_1}?")
		}
	}
}

text BerryTree_Text_PickedTheBerry {
	poryswitch(LANGUAGE) {
		EN {
			format("{PLAYER} picked up {STR_VAR_2} {STR_VAR_1}.")
		}
		ES {
			format("{PLAYER} recogió {STR_VAR_2} {STR_VAR_1}.")
		}
	}
}

text BerryTree_Text_PutAwayBerry {
	poryswitch(LANGUAGE) {
		EN {
			format("The soil returned to it's soft, fertile state.")
		}
		ES {
		format("El suelo regresó a su estado fértil y blando.")
		}
	}
}

text BerryTree_Text_BerryPocketFull {
	poryswitch(LANGUAGE) {
		EN {
			format("The BAG's BERRIES POCKET is full.\p"
			"The {STR_VAR_1} couldn't be taken.")
		}
		ES {
			format("El Bolsillo de Bayas está lleno.\p"
			"No entra nada más.")
		}
	}
}

text BerryTree_Text_BerryLeftUnpicked {
	poryswitch(LANGUAGE) {
		EN {
			format("{PLAYER} left the {STR_VAR_1}\n"
		"unpicked.")
		}
		ES {
		format("{PLAYER} no recogió la\n{STR_VAR_1}")
		}
	}
}

text BerryTree_Text_WantToWater {
	poryswitch(LANGUAGE) {
		EN {
			format("Want to water it with the WAILMER PAIL?")
		}
		ES {
			format("¿Quieres usar la Wailmegadera?")
		}
	}
}

text BerryTree_Text_WateredTheBerry {
	poryswitch(LANGUAGE) {
		EN {
			format("{PLAYER} watered the {STR_VAR_1}.")
		}
		ES {
			format("{PLAYER} regó la {STR_VAR_1}.")
		}
	}
}

text BerryTree_Text_PlantIsDelighted {
	poryswitch(LANGUAGE) {
		EN {
			format("The plant seems to be delighted.")
		}
		ES {
			format("¡Ya está! ¡Toda contenta!")
		}
	}
}

text BerryTree_Text_ExclamationPoint {
	format("!")
}

text BerryTree_Text_MulchWasAdded {
	poryswitch(LANGUAGE) {
		EN {
			format("You mixed the {STR_VAR_2} with the soil.\p"
			"Choose a Berry to plant.")
		}
		ES {
			format("Abonó el suelo con {STR_VAR_2}.\n"
			"Elige una baya para plantar.")
		}
	}
}

raw `

BerryTreeScript::
	special ObjectEventInteractionGetBerryTreeData
	switch VAR_0x8004
	case BERRY_STAGE_SPARKLING, BerryTree_EventScript_Sparkling
	case BERRY_STAGE_NO_BERRY, BerryTree_EventScript_CheckSoil
	case BERRY_STAGE_PLANTED, BerryTree_EventScript_CheckBerryStage1
	case BERRY_STAGE_SPROUTED, BerryTree_EventScript_CheckBerryStage2
	case BERRY_STAGE_TALLER, BerryTree_EventScript_CheckBerryStage3
	case BERRY_STAGE_FLOWERING, BerryTree_EventScript_CheckBerryStage4
	case BERRY_STAGE_BERRIES, BerryTree_EventScript_CheckBerryFullyGrown
	end

BerryTree_EventScript_Sparkling::
	lockall
	message BerryTree_Text_ExclamationPoint
	waitmessage
	waitbuttonpress
	releaseall
	end

BerryTree_EventScript_CheckSoil::
	lock
	faceplayer
	specialvar VAR_RESULT, PlayerHasBerries
	goto_if_eq VAR_RESULT, TRUE, BerryTree_EventScript_HasMulch
	message BerryTree_Text_ItsSoftLoamySoil
	waitmessage
	waitbuttonpress
	release
	end

BerryTree_EventScript_HasMulch::
	specialvar VAR_RESULT, PlayerHasMulch
	goto_if_eq VAR_RESULT, TRUE, BerryTree_EventScript_WantToMulch
	goto_if_eq VAR_RESULT, FALSE, BerryTree_EventScript_WantToPlant
	release
	end

BerryTree_EventScript_WantToPlant::
	msgbox BerryTree_Text_WantToPlant, MSGBOX_YESNO
	goto_if_eq VAR_RESULT, YES, BerryTree_EventScript_ChooseBerryToPlant
	goto_if_eq VAR_RESULT, NO, BerryTree_EventScript_CancelPlanting
	end

BerryTree_EventScript_WantToMulch::
	msgbox BerryTree_Text_WantToMulch, MSGBOX_YESNO
	goto_if_eq VAR_RESULT, YES, BerryTree_EventScript_ChooseMulchToAdd
	goto_if_eq VAR_RESULT, NO, BerryTree_EventScript_WantToPlantShort
	end

BerryTree_EventScript_WantToPlantShort::
	msgbox BerryTree_Text_WantToPlantShort, MSGBOX_YESNO
	goto_if_eq VAR_RESULT, YES, BerryTree_EventScript_ChooseBerryToPlant
	goto_if_eq VAR_RESULT, NO, BerryTree_EventScript_CancelPlanting
	end

BerryTree_EventScript_ChooseBerryToPlant::
	fadescreen FADE_TO_BLACK
	closemessage
	special Bag_ChooseBerry
	waitstate
	goto_if_eq VAR_ITEM_ID, 0, BerryTree_EventScript_CancelPlanting
	removeitem VAR_ITEM_ID
	call BerryTree_EventScript_PlantBerry

BerryTree_EventScript_CancelPlanting::
	release
	end

BerryTree_EventScript_ChooseMulchToAdd::
	fadescreen FADE_TO_BLACK
	closemessage
	chooseitem ITEMS_POCKET
	switch VAR_ITEM_ID
	case ITEM_GROWTH_MULCH, BerryTree_EventScript_ChooseMulchToAdd2
	case ITEM_DAMP_MULCH, BerryTree_EventScript_ChooseMulchToAdd2
	case ITEM_STABLE_MULCH, BerryTree_EventScript_ChooseMulchToAdd2
	case ITEM_RICH_MULCH, BerryTree_EventScript_ChooseMulchToAdd2
	case ITEM_SURPRISE_MULCH, BerryTree_EventScript_ChooseMulchToAdd2
	case ITEM_AMAZE_MULCH, BerryTree_EventScript_ChooseMulchToAdd2
	goto BerryTree_EventScript_CancelPlanting
BerryTree_EventScript_ChooseMulchToAdd2::
	copyvar VAR_0x8004, VAR_ITEM_ID
	removeitem VAR_ITEM_ID
	bufferitemname STR_VAR_2, VAR_0x8004
	message BerryTree_Text_MulchWasAdded
	waitmessage
	waitbuttonpress
	goto BerryTree_EventScript_ChooseBerryToPlant
	end

BerryTree_EventScript_CheckBerryStage1::
	lockall
	special ObjectEventInteractionGetBerryCountString
	message BerryTree_Text_BerryGrowthStage1
	waitmessage
	waitbuttonpress
	goto BerryTree_EventScript_WantToWater

BerryTree_EventScript_CheckBerryStage2::
	lockall
	special ObjectEventInteractionGetBerryName
	message BerryTree_Text_BerryGrowthStage2
	waitmessage
	waitbuttonpress
	goto BerryTree_EventScript_WantToWater

BerryTree_EventScript_CheckBerryStage3::
	lockall
	special ObjectEventInteractionGetBerryName
	message BerryTree_Text_BerryGrowthStage3
	waitmessage
	waitbuttonpress
	goto BerryTree_EventScript_WantToWater

BerryTree_EventScript_CheckBerryStage4::
	call BerryTree_EventScript_GetCareAdverb
	lockall
	special ObjectEventInteractionGetBerryName
	message BerryTree_Text_BerryGrowthStage4
	waitmessage
	waitbuttonpress
	goto BerryTree_EventScript_WantToWater

@ VAR_0x8005 here is the number of times watered
@ Buffered by ObjectEventInteractionGetBerryTreeData
BerryTree_EventScript_GetCareAdverb::
	goto_if_eq VAR_0x8005, 0, BerryTree_EventScript_SetAdverbPoor
	goto_if_eq VAR_0x8005, 4, BerryTree_EventScript_SetAdverbGreat
	bufferstring STR_VAR_2, BerryTree_Text_CareAdverbGood
	return

BerryTree_EventScript_SetAdverbGreat::
	bufferstring STR_VAR_2, BerryTree_Text_CareAdverbGreat
	return

BerryTree_EventScript_SetAdverbPoor::
	bufferstring STR_VAR_2, BerryTree_Text_CareAdverbPoor
	return

@ VAR_0x8006 here is the number of berries
BerryTree_EventScript_CheckBerryFullyGrown::
	buffernumberstring STR_VAR_2, VAR_0x8006
	lock
	faceplayer
	special ObjectEventInteractionGetBerryCountString
	msgbox BerryTree_Text_WantToPick, MSGBOX_YESNO
	goto_if_eq VAR_RESULT, YES, BerryTree_EventScript_PickBerry
	goto_if_eq VAR_RESULT, NO, BerryTree_EventScript_CancelPickingBerry

BerryTree_EventScript_PickBerry::
	special ObjectEventInteractionPickBerryTree
	goto_if_eq VAR_0x8004, 0, BerryTree_EventScript_BerryPocketFull
	special IncrementDailyPickedBerries
	special ObjectEventInteractionRemoveBerryTree
	message BerryTree_Text_PickedTheBerry
	delay 10
	showberrydesc
	playfanfare MUS_OBTAIN_BERRY
	waitmessage
	waitfanfare
	waitbuttonpress
	message BerryTree_Text_PutAwayBerry
	waitmessage
	waitbuttonpress
	hideitemdesc
	release
	end

BerryTree_EventScript_BerryPocketFull::
	message BerryTree_Text_BerryPocketFull
	waitmessage
	waitbuttonpress
	release
	end

BerryTree_EventScript_CancelPickingBerry::
	message BerryTree_Text_BerryLeftUnpicked
	waitmessage
	waitbuttonpress
	release
	end

BerryTree_EventScript_ItemUsePlantBerry::
	lockall
	special ObjectEventInteractionGetBerryTreeData
	call BerryTree_EventScript_PlantBerry
	releaseall
	end

BerryTree_EventScript_WantToWater::
	checkitem ITEM_WAILMER_PAIL
	goto_if_eq VAR_RESULT, FALSE, BerryTree_EventScript_DontWater
	special ObjectEventInteractionGetBerryName
	msgbox BerryTree_Text_WantToWater, MSGBOX_YESNO
	goto_if_eq VAR_RESULT, YES, BerryTree_EventScript_WaterBerry
	goto_if_eq VAR_RESULT, NO, BerryTree_EventScript_DontWater

BerryTree_EventScript_DontWater::
	releaseall
	end

BerryTree_EventScript_ItemUseWailmerPail::
	special ObjectEventInteractionGetBerryTreeData
	lockall
BerryTree_EventScript_WaterBerry::
	special ObjectEventInteractionGetBerryName
	message BerryTree_Text_WateredTheBerry
	waitmessage
	special ObjectEventInteractionWaterBerryTree
	special DoWateringBerryTreeAnim
	waitstate
	message BerryTree_Text_PlantIsDelighted
	waitmessage
	waitbuttonpress
	releaseall
	end

BerryTree_EventScript_PlantBerry::
	special ObjectEventInteractionPlantBerryTree
	incrementgamestat GAME_STAT_PLANTED_BERRIES
	special IncrementDailyPlantedBerries
	special ObjectEventInteractionGetBerryCountString
	message BerryTree_Text_PlantedOneBerry
	waitmessage
	waitbuttonpress
	return

`