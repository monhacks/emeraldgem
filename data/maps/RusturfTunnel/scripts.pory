const  LOCALID_WANDAS_BF = 1
const  LOCALID_BRINEY = 5
const  LOCALID_GRUNT = 6
const  LOCALID_PEEKO = 7
const  LOCALID_WANDA = 10

mapscripts RusturfTunnel_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
        if (var(VAR_RUSTURF_TUNNEL_STATE) == 2) {
            setobjectxyperm (LOCALID_PEEKO, 13, 4)
            setobjectxyperm (LOCALID_GRUNT, 13, 5)
            }
        }
	MAP_SCRIPT_ON_FRAME_TABLE [
     VAR_RUSTURF_TUNNEL_STATE, 4: RusturfTunnel_EventScript_ClearTunnelScene
     VAR_RUSTURF_TUNNEL_STATE, 5: RusturfTunnel_EventScript_ClearTunnelScene 
     ]
}



script RusturfTunnel_EventScript_Wanda {
	lock
	faceplayer
	msgbox (format("Mi novio estaba cavando este túnel por su cuenta, pero hubo un derrumbe que lastimó sus manos y no puede seguir cavando."))
	closemessage
	applymovement (VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	waitmovement (0)
	release
	end
}

script RusturfTunnel_EventScript_WandasBoyfriend {
	lock
	faceplayer
	msgbox (format("¡ARGH! {PAUSE 2}¡ROAR!\p¡Ese lindo hombre me detuvo cuando estaba tan cerca………!\pY ahora, un derrumbe me lastimó las manos.\pQuiero llegar con mi novia para hablarle de él."))
	closemessage
	applymovement (VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	waitmovement (0)
	release
	end
}

script RusturfTunnel_EventScript_ClearTunnelScene {
    lockall
    switch (var(VAR_TEMP_1)) {
        case 1: 
            call (RusturfTunnel_EventScript_FaceWandasBoyfriend1)
        case 2: 
            call (RusturfTunnel_EventScript_FaceWandasBoyfriend2)
        case 3: 
            call (RusturfTunnel_EventScript_FaceWandasBoyfriend3)
        }
    call (RusturfTunnel_EventScript_WandasBoyfriendNotice)
    namebox (Riley)
    msgbox(format("¡ROAR!\nE-eh, quiero decir, {PAUSE 2}¡wow!\p¡Destruíste esa roca que bloqueaba el camino! {PAUSE 2}¡Ni mis enormes músculos de constructor podían destruírla!\pComo agradecimiento de crear un atajo para ver a mi novia, ten esto."))
    hidenamebox
    switch (var(VAR_TEMP_1)) {
        case 2:
        case 3:
            closemessage
            applymovement (LOCALID_WANDAS_BF, RusturfTunnel_Movement_WandasBoyfriendApproachPlayer)
            waitmovement (0)
    }
    giveitem (ITEM_AGGRONITE)
    namebox(Riley)
    msgbox(format("¡Es una Aggronita!\pEsa es una megapiedra que puede hacer {COLOR BLUE}megaevolucionar a un Aggron{COLOR DARK_GRAY}.\p¡Gracias a tí podré hablar y ver a mi novia cuando quiera!"))
    setflag (FLAG_RECEIVED_HM04)
    closemessage
    hidenamebox
    switch (var(VAR_TEMP_1)) {
        case 1:
            call (RusturfTunnel_EventScript_BoyfriendApproachWanda1)
        case 2:
            call (RusturfTunnel_EventScript_BoyfriendApproachWanda2)
        case 3:
            call (RusturfTunnel_EventScript_BoyfriendApproachWanda3)
    }
    namebox(Riley)
    msgbox(format("¡Amor! {PAUSE 2}¡Hola!\nNnn… {PAUSE 2}¡ROOOAARR! {PAUSE 2}¡Estoy tan feliz!"))
    hidenamebox
    namebox(Wanda)
    msgbox(format("¡Oh, Riley, amor mío!\nAl fin nos vemos otra vez."))
    hidenamebox
    namebox(Riley)
    msgbox(format("¡Amorcito!\nLa cosa más maravillosa ha pasado, no lo creerás.\p¡Alguien más ha reconocido el esfuerzo que hice para mantener estos músculos!"))
    hidenamebox
    namebox(Wanda)
    msgbox(format("¡Qué cosa tan maravillosa, mi amor!\p¡Podemos hablar más de esa persona en mi casa!\pMe gustaría conocer gente nueva que reconozca el trabajo duro."))
    hidenamebox
    switch (var(VAR_TEMP_1)) {
        case 1:
            call (RusturfTunnel_EventScript_WandaAndBoyfriendExit1)
        case 2:
        case 3:
            call (RusturfTunnel_EventScript_WandaAndBoyfriendExit)
    }
    call (RusturfTunnel_EventScript_SetRusturfTunnelOpen)
    releaseall
    end
}

script RusturfTunnel_EventScript_BoyfriendApproachWanda1 {
    applymovement (OBJ_EVENT_ID_PLAYER, RusturfTunnel_Movement_PlayerWatchBoyfriend1)
    applymovement (LOCALID_WANDAS_BF, RusturfTunnel_Movement_BoyfriendApproachWanda1)
    waitmovement (0)
    return
}

script RusturfTunnel_EventScript_BoyfriendApproachWanda2 {
	applymovement (OBJ_EVENT_ID_PLAYER, RusturfTunnel_Movement_PlayerWatchBoyfriend)
	applymovement (LOCALID_WANDAS_BF, RusturfTunnel_Movement_BoyfriendApproachWanda)
	waitmovement (0)
	applymovement (LOCALID_WANDA, Common_Movement_WalkInPlaceFasterDown)
	waitmovement (0)
	return
}

script RusturfTunnel_EventScript_BoyfriendApproachWanda3 {
	applymovement (OBJ_EVENT_ID_PLAYER, RusturfTunnel_Movement_PlayerWatchBoyfriend)
	applymovement (LOCALID_WANDAS_BF, RusturfTunnel_Movement_BoyfriendApproachWanda)
	waitmovement (0)
	applymovement (LOCALID_WANDA, Common_Movement_WalkInPlaceFasterDown)
	waitmovement (0)
	return
}

script RusturfTunnel_EventScript_FaceWandasBoyfriend1 {
	applymovement (LOCALID_WANDAS_BF, Common_Movement_WalkInPlaceFasterUp)
	waitmovement (0)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterDown)
	waitmovement (0)
	return
}

script RusturfTunnel_EventScript_FaceWandasBoyfriend2 {
	applymovement (LOCALID_WANDAS_BF, RusturfTunnel_Movement_BoyfriendFaceRight)
	waitmovement (0)
	return
}

script RusturfTunnel_EventScript_FaceWandasBoyfriend3 {
	return
    }

script RusturfTunnel_EventScript_WandasBoyfriendApproachPlayer {
	closemessage
	applymovement (LOCALID_WANDAS_BF, RusturfTunnel_Movement_WandasBoyfriendApproachPlayer)
	waitmovement (0)
	return
}

script RusturfTunnel_EventScript_WandaAndBoyfriendExit1 {
	applymovement (LOCALID_WANDA, RusturfTunnel_Movement_WandaExit1)
	applymovement (LOCALID_WANDAS_BF, RusturfTunnel_Movement_WandasBoyfriendExit1)
	waitmovement (0)
    removeobject (LOCALID_WANDA)
    removeobject (LOCALID_WANDAS_BF)
    playse (SE_EXIT)
	return
}

script RusturfTunnel_EventScript_WandaAndBoyfriendExit {
	applymovement (OBJ_EVENT_ID_PLAYER, RusturfTunnel_Movement_PlayerWatchWandaExit)
	applymovement (LOCALID_WANDA, RusturfTunnel_Movement_WandaExit)
	applymovement (LOCALID_WANDAS_BF, RusturfTunnel_Movement_WandasBoyfriendExit)
    waitmovement (0)
    removeobject (LOCALID_WANDA)
    removeobject (LOCALID_WANDAS_BF)
    setflag (FLAG_HIDE_RUSTURF_TUNNEL_WANDAS_BOYFRIEND)
    setflag (FLAG_HIDE_RUSTURF_TUNNEL_WANDA)
    playse (SE_EXIT)
	return
}

script RusturfTunnel_EventScript_WandasBoyfriendNotice {
	playse (SE_PIN)
	applymovement (LOCALID_WANDAS_BF, Common_Movement_ExclamationMark)
	waitmovement (0)
	applymovement (LOCALID_WANDAS_BF, Common_Movement_Delay48)
	waitmovement (0)
	return
}

movement RusturfTunnel_Movement_WandaExit1 {
	walk_right * 4
	walk_down * 7
}

movement RusturfTunnel_Movement_WandaExit {
	walk_right * 4
	walk_down * 7
}

movement RusturfTunnel_Movement_PlayerWatchWandaExit {
	delay_8
	walk_in_place_faster_up
	delay_16 * 2
	walk_in_place_faster_right
	}

movement RusturfTunnel_Movement_PlayerWatchBoyfriend1 {
	walk_left
	walk_in_place_faster_right
	}

movement RusturfTunnel_Movement_PlayerWatchBoyfriend {
	walk_right
	walk_in_place_faster_left
	}

movement RusturfTunnel_Movement_BoyfriendFaceRight {
	walk_up
	walk_in_place_faster_right
	}

movement RusturfTunnel_Movement_WandasBoyfriendExit1 {
	walk_right * 5
	walk_down * 7
	}

movement RusturfTunnel_Movement_WandasBoyfriendExit {
	walk_up
	walk_right * 4
	walk_down * 7
	}

movement RusturfTunnel_Movement_WandasBoyfriendApproachPlayer {
	walk_right
	}

movement RusturfTunnel_Movement_BoyfriendApproachWanda1 {
	walk_in_place_fast_up * 2
	walk_fast_up
	walk_fast_right
	}

movement RusturfTunnel_Movement_BoyfriendApproachWanda {
	walk_in_place_fast_right * 2
	walk_fast_right
	walk_in_place_faster_up
	}

script RusturfTunnel_EventScript_TunnelBlockagePos1 {
	setvar (VAR_TEMP_1, 1)
	end
    }

script RusturfTunnel_EventScript_TunnelBlockagePos2 {
	setvar (VAR_TEMP_1, 2)
	end
    }

script RusturfTunnel_EventScript_TunnelBlockagePos3 {
	setvar (VAR_TEMP_1, 3)
	end
    }

script RusturfTunnel_EventScript_Peeko {
	lock
	faceplayer
	waitse
	playmoncry (SPECIES_WINGULL, CRY_MODE_NORMAL)
	msgbox(format("¡Pii pihyoh!"))
	waitmoncry
	release
	end
}

script RusturfTunnel_EventScript_Grunt {
	lock
	faceplayer
	playbgm (MUS_ENCOUNTER_AQUA, FALSE)
    namebox(Aqua)
	msgbox(format("¡Argh, este Peeko resultó ser una molestia!\pMe ordenaron robar las {COLOR BLUE}Partes{COLOR DARK_GRAY}, detener la construcción y escapar, pero ese tipo excavador me persiguió hasta que robé este Peeko y me burlé de sus músculos.\p………\p¿Sigues aquí?\nEntonces, {PAUSE 2}¡Peleemos!"))
	hidenamebox
    trainerbattle_no_intro (TRAINER_GRUNT_RUSTURF_TUNNEL, RusturfTunnel_Text_GruntDefeat)
	namebox(Aqua)
    msgbox (format("No comprendo.\pEl {COLOR BLUE}Jefecito{COLOR DARK_GRAY} me dijo que sería tarea fácil, {PAUSE 2}¡Pero no lo es!\p¡ARGH! {PAUSE 2}¡ROAR!\p¡Tendré que hacer esto con trucos sucios!"))
    hidenamebox
    closemessage
    playse (SE_PIN)
    applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
    applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_Delay48)
    waitmovement (0)
	special (HealPlayerParty)
    setflag (FLAG_STARTED_BOSS)
	special (TakeAwayItemsAndBerries)
	special (SavePlayerParty)
	deletepokemon (1)
    message (format("¡Te han robado la mochila!\n¡Ahora sólo podrás usar el Pokémon que tenías a mano!"))
    waitmessage
    closemessage
    applymovement (LOCALID_GRUNT, RusturfTunnel_Movement_GruntNervous)
    waitmovement (0)
    playbgm (MUS_HELP, FALSE)
    namebox(Aqua)
    msgbox(format("¡NO IMPORTA NADA, YO ME VOY!"))
    hidenamebox
    closemessage
    applymovement (OBJ_EVENT_ID_PLAYER, RusturfTunnel_Movement_PushPlayerAsideForGrunt)
    applymovement (LOCALID_GRUNT, RusturfTunnel_Movement_GruntEscape)
    waitmovement(0)
    namebox(Unknown)
    msgbox(format("¡No te preocupes, yo lo detengo aquí!"))
    closemessage
    addobject (LOCALID_BRINEY)
    hidenamebox
    namebox(Aqua)
    msgbox(format("¡N-NO!\n¡ROAR!"))
    hidenamebox
    closemessage
    delay (32)
    applymovement (OBJ_EVENT_ID_PLAYER, RusturfTunnel_Movement_PlayerPursue)
    waitmovement (0)
    applymovement (LOCALID_GRUNT, Common_Movement_FacePlayer)
    namebox(Aqua)
    msgbox(format("¡A-ARGH!\p¡Tch, acorralado!\p¡B-Bueno, si tanto quieren sus Partes, pelea de nuevo, cobarde!"))
    trainerbattle_no_intro (TRAINER_GRUNT_RUSTURF_TUNNEL, RusturfTunnel_Text_GruntDefeat2)
    savepartofteam
	special (LoadPlayerParty)
	loadpartofteam
	special (GiveBackItemsAndBerries)
	clearflag (FLAG_STARTED_BOSS)
    namebox(Aqua)
    msgbox(format("¡N-NO!\n¡No puede ser!\l¡ARGH! {PAUSE 2}¡Aquí tienes denuevo tus inútiles Pokémon!"))
    hidenamebox
    message (format("¡Te han devuelto la mochila!"))
    namebox(Aqua)
    msgbox(format("¡A-ARGH! {PAUSE 2}¡ROAR! {PAUSE 2}¡NOO!\p¡Tch, aquí tienen las partes!\nDe todas formas, {PAUSE 2}¡Nuestro plan es infalible, mua ja ja!"))
    hidenamebox
	giveitem (ITEM_DEVON_PARTS, 1)
	closemessage
    applymovement (LOCALID_GRUNT, Common_Movement_FaceDown)
    waitmovement (0)
    delay (16)
    namebox(Aqua)
    msgbox(format("………\pEsta………\nNo fue una experiencia que quiera que se repita."))
    hidenamebox
    applymovement (LOCALID_GRUNT, Common_Movement_WalkInPlaceDown)
    waitmovement (0)
    delay (16)
    namebox(Aqua)
    msgbox(format("………\pA lo mejor………\nSer un ladrón no sea lo mío."))
    hidenamebox
    closemessage
    delay (16)
    applymovement (LOCALID_BRINEY, RusturfTunnel_Movement_BrineyMove)
    waitmovement (0)
    applymovement (LOCALID_GRUNT, RusturfTunnel_Movement_GruntRegrets)
    waitmovement (0)
    namebox(Aqua)
    msgbox(format("………\pYo………\nLo lamento, señor.\pNo. Lo que hice no tiene perdón.\p………\pDebería disculparme con el hombre que insulté antes, y explicarle que… No era un insulto…"))
    hidenamebox
    closemessage
    applymovement (LOCALID_GRUNT, RusturfTunnel_Movement_GruntExit)
    applymovement (LOCALID_BRINEY, Common_Movement_FaceDown)
    waitmovement(0)
    playse (SE_EXIT)
	removeobject (LOCALID_GRUNT)
	delay (50)
	applymovement (LOCALID_PEEKO, RusturfTunnel_Movement_ApproachPeeko1)
	waitmovement (0)
    playmoncry (SPECIES_WINGULL, CRY_MODE_NORMAL)
	waitmoncry
    playse (SE_PIN)
    applymovement (LOCALID_BRINEY, Common_Movement_ExclamationMark)
    applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
    applymovement (LOCALID_BRINEY, Common_Movement_FaceUp)
    applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
    waitmovement (0)
    delay (8)
    namebox (Briney)
    msgbox("¡P-Peeko!")
    hidenamebox
	applymovement (LOCALID_BRINEY, RusturfTunnel_Movement_BrineyApproachPeeko1)
	waitmovement (0)
    namebox (Briney)
	msgbox(format("¡Oh, Peeko!\n¡Te extrañé tanto, mi chiquito!"))
    hidenamebox
	applymovement (LOCALID_BRINEY, Common_Movement_FacePlayer)
	waitmovement (0)
	namebox (Briney)
    msgbox(format("¡Muchísimas gracias!\nPeeko, vamos a casa."))
	closemessage
    hidenamebox
	playmoncry (SPECIES_WINGULL, CRY_MODE_NORMAL)
	waitbuttonpress
	waitmoncry
	applymovement (OBJ_EVENT_ID_PLAYER, RusturfTunnel_Movement_PlayerMoveAsideForBriney)
	applymovement (LOCALID_BRINEY, RusturfTunnel_Movement_BrineyExit)
	applymovement (LOCALID_PEEKO, RusturfTunnel_Movement_PeekoExit)
    delay (64)
	removeobject (LOCALID_BRINEY)
    playse (SE_EXIT)
    delay (16)
    playse (SE_EXIT)
	removeobject (LOCALID_PEEKO)
	clearflag (FLAG_DEVON_GOODS_STOLEN)
	setflag (FLAG_RECOVERED_DEVON_GOODS)
	setvar (VAR_RUSTBORO_CITY_STATE, 4)
	setvar (VAR_BRINEY_HOUSE_STATE, 1)
	setflag (FLAG_HIDE_ROUTE_116_MR_BRINEY)
	release
	end
}

text RusturfTunnel_Text_GruntDefeat2 {
    "¡ARGH! {PAUSE 2}¡ROAR!\p¡DÉJAME GANARTE!"
}

movement RusturfTunnel_Movement_ApproachPeeko1 {
    walk_left * 9
    walk_down * 2
}

movement RusturfTunnel_Movement_GruntExit {
    walk_down
    walk_down
}

movement RusturfTunnel_Movement_GruntNervous {
    walk_in_place_fast_right
    delay_4
    walk_in_place_fast_left
    delay_4
    walk_in_place_fast_right
    delay_4
    walk_in_place_fast_left
    delay_16
    delay_8
    delay_4
    jump_in_place_left
    jump_in_place_left
}

movement RusturfTunnel_Movement_PushPlayerAsideForGrunt {
	face_down
	lock_facing_direction
	walk_up
	unlock_facing_direction
	walk_in_place_faster_left
}

movement RusturfTunnel_Movement_PlayerMoveAsideForBriney {
	walk_right
	walk_in_place_faster_down
}

movement RusturfTunnel_Movement_GruntEscape {
	walk_fast_left * 9
    walk_fast_down * 4
	}
movement RusturfTunnel_Movement_PlayerPursue {
    walk_fast_down
    walk_fast_left * 8
    walk_fast_down * 3
}

movement RusturfTunnel_Movement_BrineyMove {
    face_right
    lock_facing_direction
    walk_left
    unlock_facing_direction
}
movement RusturfTunnel_Movement_GruntRegrets {
    walk_down
    delay_16
    delay_16
    walk_in_place_left
}

movement RusturfTunnel_Movement_BrineyApproachPeeko1 {
	walk_fast_up * 3
    walk_fast_right
    walk_in_place_fast_up
}

movement RusturfTunnel_Movement_BrineyExit {
	walk_down * 4
}

movement RusturfTunnel_Movement_PeekoExit {
	walk_down * 5
}

script RusturfTunnel_EventScript_Mike {
	trainerbattle_single (TRAINER_MIKE_2, RusturfTunnel_Text_MikeIntro, RusturfTunnel_Text_MikeDefeat)
	msgbox (format("¡Esta cueva es muy pequeña!"))
	end
    }


text RusturfTunnel_Text_GruntDefeat {
    "¡ARGH! {PAUSE 2}¡ROAR!\p¿Porqué no pueden dejarme\nterminar esta misión?"
}



text RusturfTunnel_Text_MikeIntro {
    "Si un hombre que está en las montañas se llama montañero, {PAUSE 2}¿Porqué los Pokémon de montañas no se llaman montañokes?"}

text RusturfTunnel_Text_MikeDefeat {
    "Mis montañokes………"}

