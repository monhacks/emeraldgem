const LOCALID_MATT   = 1
const LOCALID_ARCHIE = 4
const LOCALID_WINONA = 6

mapscripts AquaHideout_B2F_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		if (var(VAR_AQUA_HIDEOUT_STATE) == 5){
			
			return
		}
		if (var(VAR_AQUA_HIDEOUT_STATE) >= 6){
			setvar(VAR_TEMP_1, 1)
			return
		}
	}
}

script AquaHideout_B2F_EventScript_MattNoticePlayerUp {
	setvar (VAR_TEMP_1, 1)
	goto (AquaHideout_B2F_EventScript_MattNoticePlayer)
}
script AquaHideout_B2F_EventScript_MattNoticePlayerDown {
	setvar (VAR_TEMP_1, 2)
	goto (AquaHideout_B2F_EventScript_MattNoticePlayer)
}
script AquaHideout_B2F_EventScript_MattNoticePlayer {
	lockall
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_FaceDown)
	playse (SE_PIN)
	applymovement (LOCALID_MATT, Common_Movement_ExclamationMark)
	waitmovement (0)
	applymovement (LOCALID_WINONA, AquaHideout_B2F_Movement_WinonaGetNear)
	namebox (Winona)
	msgbox(format("¡Hola, Archie! ¡Hola, Matt!\n¿Qué ha sido de us{PAUSE 20}- "), MSGBOX_INTERRUPT)
	hidenamebox
	delay (64)
	applymovement (LOCALID_MATT, AquaHideout_B2F_Movement_MattGetInWay)
	namebox (Matt)
	waitmovement (0)
	msgbox(format("¡No te acerques más! ¡O voy a tener que atacarte!"))
	namebox (Archie)
	msgbox(format("Matt, esto no es ne{PAUSE 20}- "),MSGBOX_INTERRUPT)
	closemessage
	hidenamebox
	namebox (Winona)
	msgbox(format("Al lado de Archie como siempre, ¿No, Matt?\nPuedo luchar contigo luego, ahora debo hablar con Archie."))
	namebox (Matt)
	msgbox(format("¡No voy a dejarte que le hagas nada a Archie! ¡Apártate!"))
	namebox (Winona)
	msgbox(format("Qué lástima, no quería tener que luchar, pero…"))
	hidenamebox
	if (var(VAR_EVIL_TEAM_SIDE) > 3){
		if (var(VAR_TEMP_1) == 1) {
			applymovement (OBJ_EVENT_ID_PLAYER, AquaHideout_B2F_Movement_PlayerHelpUp)
		}
		else {
			applymovement (OBJ_EVENT_ID_PLAYER, AquaHideout_B2F_Movement_PlayerHelpDown)
		}
		waitmovement (0)
		namebox (Matt)
		msgbox(format("¿Nos vas a ayudar?"))
		namebox(Archie)
		msgbox(format("Será sólo un{A} niñ{OA}, pero ha estado de nuestro lado antes… Aún así, Matt, esto no es necesario…"))
		call (AquaHideout_B2F_EventScript_WinonaPrepare)
		// call (AquaHideout_B2F_EventScript_LostMultiBattle)
		multi_fixed_2_vs_1 (TRAINER_WINONA_AQUA_HIDEOUT, "¡Impresionante!\nNo me esperaba menos\pdel Equipo Aqua.", TRAINER_MATT_PARTNER, TRAINER_BACK_PIC_RED)
		specialvar (VAR_RESULT, GetBattleOutcome)
		compare (VAR_RESULT, B_OUTCOME_LOST)
		goto_if_eq (MtChimney_EventScript_LostMultiBattle)
	}
	elif (var(VAR_EVIL_TEAM_SIDE) < 3){
		if (var(VAR_TEMP_1) == 1) {
			applymovement (OBJ_EVENT_ID_PLAYER, AquaHideout_B2F_Movement_PlayerAgainstUp)
		}
		else {
			applymovement (OBJ_EVENT_ID_PLAYER, AquaHideout_B2F_Movement_PlayerAgainstDown)
		}
		waitmovement (0)
		namebox (Matt)
		msgbox(format("Así que estás en nuetra contra después de todo…"))
		namebox(Archie)
		msgbox(format("No me sorprende… Aún así, Matt, esto no es necesario…"))
		call (AquaHideout_B2F_EventScript_WinonaPrepare)
		multi_fixed_2_vs_1 (TRAINER_MATT, "¡N-no! Archie, te defraudé…", TRAINER_WINONA_AQUA_HIDEOUT, TRAINER_BACK_PIC_RED)
		specialvar (VAR_RESULT, GetBattleOutcome)
		compare (VAR_RESULT, B_OUTCOME_LOST)
		goto_if_eq (MtChimney_EventScript_LostMultiBattle)
		}
	else {
		call (AquaHideout_B2F_EventScript_WinonaPrepare)
	}
	
	releaseall
	end
}

script AquaHideout_B2F_EventScript_LostMultiBattle {
	fadescreen (FADE_TO_BLACK)
	special (SetCB2WhiteOut)
	releaseall
	end
}

script AquaHideout_B2F_EventScript_WinonaPrepare {
	namebox (Winona)
	msgbox(format("¡Prepárense!"))
	fadescreen (FADE_TO_WHITE)
	fadescreen (FADE_FROM_WHITE)
	applymovement (LOCALID_ARCHIE, Common_Movement_ExclamationMark)
	applymovement (LOCALID_MATT, Common_Movement_ExclamationMark)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	playse (SE_PIN)
	waitmovement(0)
	fadescreen (FADE_TO_WHITE)
}

movement AquaHideout_B2F_Movement_WinonaGetNear {
	walk_left * 3
}

movement AquaHideout_B2F_Movement_MattGetInWay {
	walk_fast_down
	walk_in_place_fast_right
}

movement AquaHideout_B2F_Movement_PlayerHelpUp {
	walk_left
	walk_down
	walk_left * 4
	walk_down
	walk_in_place_fast_down
	delay_8
	walk_in_place_fast_right
}
movement AquaHideout_B2F_Movement_PlayerHelpDown {
	walk_left
	walk_down
	walk_left * 4
	walk_in_place_fast_down
	delay_8
	walk_in_place_fast_right
}

movement AquaHideout_B2F_Movement_PlayerAgainstUp {
	walk_left
	walk_down
	walk_left * 3
	walk_down
	walk_in_place_fast_down
	delay_8
	walk_in_place_fast_left
}
movement AquaHideout_B2F_Movement_PlayerAgainstDown {
	walk_left
	walk_down
	walk_left * 3
	walk_in_place_fast_down
	delay_8
	walk_in_place_fast_left
}

raw `
.set LOCALID_MATT,   1
.set LOCALID_SUBMARINE,   1
.set LOCALID_ARCHIE, 4
.set LOCALID_WINONA, 6



AquaHideout_B2F_EventScript_Matt::
	trainerbattle_single TRAINER_MATT, AquaHideout_B2F_Text_MattIntro, AquaHideout_B2F_Text_MattDefeat, AquaHideout_B2F_EventScript_SubmarineEscape
	msgbox AquaHideout_B2F_Text_MattPostBattle, MSGBOX_DEFAULT
	release
	end

AquaHideout_B2F_EventScript_SubmarineEscape::
	setvar VAR_0x8008, LOCALID_MATT
	setvar VAR_0x8009, LOCALID_SUBMARINE
	applymovement VAR_0x8008, Common_Movement_WalkInPlaceFasterLeft
	waitmovement 0
	delay 20
	applymovement VAR_0x8008, Common_Movement_FacePlayer
	waitmovement 0
	msgbox AquaHideout_B2F_Text_OurBossGotThroughHisPreparations, MSGBOX_DEFAULT
	closemessage
	applymovement VAR_0x8008, Common_Movement_WalkInPlaceFasterLeft
	applymovement VAR_0x8009, AquaHideout_B2F_Movement_SumbarineDepartLeft
	waitmovement 0
	removeobject VAR_0x8009
	delay 20
	applymovement VAR_0x8008, Common_Movement_FacePlayer
	waitmovement 0
	msgbox AquaHideout_B2F_Text_MattPostBattle, MSGBOX_DEFAULT
	setflag FLAG_TEAM_AQUA_ESCAPED_IN_SUBMARINE
	setflag FLAG_HIDE_LILYCOVE_CITY_AQUA_GRUNTS
	release
	end

AquaHideout_B2F_Movement_SumbarineDepartLeft:
	walk_left
	walk_left
	walk_left
	walk_left
	step_end

@ Unused
AquaHideout_B2F_Movement_SumbarineDepartRight:
	walk_right
	walk_right
	walk_right
	walk_right
	step_end

AquaHideout_B2F_EventScript_Grunt4::
	trainerbattle_single TRAINER_GRUNT_AQUA_HIDEOUT_4, AquaHideout_B2F_Text_Grunt4Intro, AquaHideout_B2F_Text_Grunt4Defeat, AquaHideout_B2F_EventScript_Grunt4Defeated
	msgbox AquaHideout_B2F_Text_Grunt4PostBattle, MSGBOX_AUTOCLOSE
	end

AquaHideout_B2F_EventScript_Grunt4Defeated::
	msgbox AquaHideout_B2F_Text_Grunt4PostBattle, MSGBOX_DEFAULT
	release
	end

AquaHideout_B2F_EventScript_Grunt6::
	trainerbattle_single TRAINER_GRUNT_AQUA_HIDEOUT_6, AquaHideout_B2F_Text_Grunt6Intro, AquaHideout_B2F_Text_Grunt6Defeat
	msgbox AquaHideout_B2F_Text_Grunt6PostBattle, MSGBOX_AUTOCLOSE
	end



AquaHideout_B2F_Text_MattIntro:
	.string "Hehehe…\p"
	.string "Got here already, did you?\n"
	.string "We underestimated you!\p"
	.string "But this is it!\p"
	.string "I'm a cut above the GRUNTS you've seen\n"
	.string "so far.\p"
	.string "I'm not stalling for time.\n"
	.string "I'm going to pulverize you!$"

AquaHideout_B2F_Text_MattDefeat:
	.string "Hehehe…\n"
	.string "So, I lost, too…$"

AquaHideout_B2F_Text_OurBossGotThroughHisPreparations:
	.string "Hehehe!\p"
	.string "While I was toying with you, our BOSS\n"
	.string "got through his preparations!$"

AquaHideout_B2F_Text_MattPostBattle:
	.string "Hehehe!\p"
	.string "Our BOSS has already gone on his way to\n"
	.string "some cave under the sea!\p"
	.string "If you're going to give chase, you'd\n"
	.string "better search the big, wide sea beyond\l"
	.string "LILYCOVE.\p"
	.string "But will you find it then?\n"
	.string "Hehehe!$"

AquaHideout_B2F_Text_Grunt4Intro:
	.string "Wahahah, I grew weary of waiting!\n"
	.string "You owe me a battle, too!$"

AquaHideout_B2F_Text_Grunt4Defeat:
	.string "Tired of waiting…\n"
	.string "Lost and dazed…$"

AquaHideout_B2F_Text_Grunt4PostBattle:
	.string "BOSS…\n"
	.string "Is this good enough?$"

AquaHideout_B2F_Text_Grunt6Intro:
	.string "Warp panels, the HIDEOUT's pride\n"
	.string "and joy!\p"
	.string "You're clueless about where you are,\n"
	.string "aren't you?\p"
	.string "Fluster and tire out the enemy, then\n"
	.string "lower the boom! That's our plan!$"

AquaHideout_B2F_Text_Grunt6Defeat:
	.string "What's wrong with you?\n"
	.string "You're not tired at all!$"

AquaHideout_B2F_Text_Grunt6PostBattle:
	.string "That reminds me… I can't remember\n"
	.string "where I put the MASTER BALL.\p"
	.string "If I fail to guard it, our BOSS will\n"
	.string "chew me out…$"

AquaHideout_B2F_Text_Grunt8Intro:
	.string "When I joined TEAM AQUA, the first\n"
	.string "thing I had to learn was how these\l"
	.string "warp panels connected.$"

AquaHideout_B2F_Text_Grunt8Defeat:
	.string "I was too occupied thinking about\n"
	.string "the warp panels…$"

AquaHideout_B2F_Text_Grunt8PostBattle:
	.string "I'll have to learn about how I can\n"
	.string "battle more effectively…$"
`
